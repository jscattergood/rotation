ext {
    user = project.hasProperty('user') ? project.user : ""
    appVersion = '0.9'
    buildVersion = project.hasProperty('buildNumber') ? appVersion + "." + project.buildNumber : appVersion
}

buildscript {
    ext {
        springBootVersion = '1.3.2.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}",
            "com.netflix.nebula:gradle-ospackage-plugin:3.4.0"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'nebula.ospackage'

jar {
    baseName = project.name
    version = project.appVersion
    doFirst {
        def config = configurations.compile.resolvedConfiguration
        def artifacts = config.resolvedArtifacts
        configurations.compile.files(Specs.convertClosureToSpec {it.group == "org.webjars"}).each { jar ->
            def artifact = artifacts.find {
                it.file.toString() == jar.absolutePath
            }

            if (artifact) {
                copy {
                    from zipTree(jar)
                    into file("${buildDir}/webjars-content/${artifact.name}")
                }

                def upStreamVersion = "${artifact.moduleVersion.id.version}"
                upStreamVersion = upStreamVersion.replaceAll(/(-[\d.-]+)/, '')

                copy {
                    from "${buildDir}/webjars-content/${artifact.name}/META-INF/resources/webjars/${artifact.name}/${upStreamVersion}",
                            "${buildDir}/webjars-content/${artifact.name}/META-INF/resources/webjars/${artifact.name}/${artifact.moduleVersion.id.version}"

                    into "${buildDir}/resources/main/static/lib/${artifact.name}"
                }
            }
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/sbuettner/maven' }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'de.infinit:spring-boot-autoconfigure-wro4j:0.0.6'
    compile 'io.reactivex:rxjava:1.0.10'
    compile 'com.sun.mail:javax.mail:1.5.2'

    compile 'org.webjars:jquery:2.2.0'
    compile 'org.webjars:bootstrap:3.3.6'
    compile 'org.webjars:angularjs:1.5.0'
    compile 'org.webjars:angular-ui-bootstrap:1.1.1-1'
    compile 'org.webjars:ui-grid:3.0.7'
    compile 'org.webjars:angular-ui-select:0.14.8'
    compile 'org.webjars:font-awesome:4.5.0'

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    runtime 'com.h2database:h2'
    runtime 'org.postgresql:postgresql'
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

distZip {
    archiveName "${project.name}-${project.buildVersion}.zip"
}

distTar {
    archiveName "${project.name}-${project.buildVersion}.tar"
}

ospackage {
    version = "${project.buildVersion}"
    user = "${project.user}"
    into "/opt/${project.name}/latest"
    from "${project.buildDir}/install/${project.name}"
    link("/opt/${project.name}/default", "/opt/${project.name}/latest")
}

buildDeb {
    dependsOn installDist
}

buildRpm {
    dependsOn installDist
    arch = NOARCH
    os = LINUX
}

startScripts {
    applicationName "${project.name}"

    doLast {
        def winScriptFile = file getWindowsScript()
        def winFileText = winScriptFile.text
        winFileText = winFileText.replaceAll('set CLASSPATH=.*', 'rem original CLASSPATH declaration replaced by:\nset CLASSPATH=%APP_HOME%\\\\lib\\\\\\*')
        winScriptFile.text = winFileText
    }
}

